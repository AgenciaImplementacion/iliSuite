apply plugin: "java"

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

compileJava.options.encoding = 'UTF-8'

def distDir = "$buildDir/dist"

repositories {
	flatDir {
		dirs 'libs'
	}
	mavenLocal()
	maven {
		url "http://jars.interlis.ch"
	}
	mavenCentral()
}

sourceSets {
	main {
		java {
			srcDirs=[
			'src',
			'src_supported_impls/ili2fgdbImpl/src',
			'src_supported_impls/ili2gpkgImpl/src',
			'src_supported_impls/ili2mssqlImpl/src',
			'src_supported_impls/ili2oraImpl/src',
			'src_supported_impls/ili2pgImpl/src']
		}
		resources {
			srcDirs=[
			'src',
			'src_supported_impls/ili2fgdbImpl/src',
			'src_supported_impls/ili2gpkgImpl/src',
			'src_supported_impls/ili2mssqlImpl/src',
			'src_supported_impls/ili2oraImpl/src',
			'src_supported_impls/ili2pgImpl/src']
		}
	}
}

dependencies {
	// libs
	implementation group: 'ch.interlis', name: 'iox-ili', version: '1.20.13'
	implementation group: 'ch.interlis', name: 'ili2c-tool', version: '5.0.2'
	implementation group: 'ch.interlis', name: 'ili2c-core', version: '5.0.2'
	implementation group: 'ch.ehi', name: 'ehisqlgen', version: '1.13.8'
	implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.9.7'
	implementation group: 'net.iharder', name: 'base64', version: '2.3.9'

	implementation name: 'richtextfx-fat-0.9.0'

	implementation name: 'ilivalidatorLib-1.11.1'

	// ili2db
	implementation name: 'ili2dbLib-4.3.0'
	implementation name: 'ili2fgdbLib-4.3.0'
	implementation name: 'ili2gpkgLib-4.3.0'
	implementation name: 'ili2mssqlLib-4.3.0'
	implementation name: 'ili2oraLib-4.3.0'
	implementation name: 'ili2pgLib-4.3.0'
	//drivers dbs
	implementation group: 'org.postgresql', name: 'postgresql', version: '42.1.4.jre6'
	implementation group: 'ch.ehi', name: 'fgdb4j', version: "1.1.1"
	implementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.8.11.2'
	implementation group: 'com.microsoft.sqlserver', name: 'mssql-jdbc', version: '6.4.0.jre7'
}

task mainJar(type: Jar) {
	baseName = 'iliSuite'
	from sourceSets.main.output
	manifest {
	attributes(
		"Main-Class": "ai.iliSuite.application.MainPreLoad",
		"Class-Path": configurations.runtimeClasspath.collect { 'ilisuite_lib/'+it.getName() }.join(' '))
	}
}

task cpDistFiles(dependsOn: mainJar, type: Copy){
	into "$distDir"
	// jar
	from("$buildDir/libs/iliSuite.jar")
	//libs
	from(configurations.compileClasspath){
		into 'ilisuite_lib'
	}
	// others
	from("docs/iliSuite_help_es.pdf")
	from ("$projectDir") {
		include "help/**", "programs/**", '.defaultConfig.properties'
	}
}

task installerWin(dependsOn: cpDistFiles, type: Exec) {
	commandLine 'ISCC', "$projectDir\\src_installer\\win\\ilisuite.iss"
	doLast {
		if(execResult.getExitValue() != 0) {
			def warningMessage = 'Remember to add the InnoSetup directory to the System\'s Path environment variable!!!'
			println warningMessage
			println warningMessage
			println warningMessage
		}
	}
}

task iliSuiteBindist(dependsOn: cpDistFiles, type: Zip) {
	baseName = 'iliSuite_bin'
	destinationDir = file("$buildDir/bindist")
	from "$distDir"
}

if(System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
	iliSuiteBindist.finalizedBy installerWin
}